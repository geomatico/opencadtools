// -*- tab-width: 4; -*-

%{
//
// Vicente Caballero Navarro
%}

%start Spline::FirstPoint
%class SplineCADTool
%package com.iver.cit.gvsig.gui.cad.tools.smc
%import com.iver.cit.gvsig.gui.cad.tools.SplineCADTool
%import java.awt.event.InputEvent
%import com.iver.andami.PluginServices

%map Spline
%%
// A task begins life in suspended animation.

FirstPoint

	Entry {
		setQuestion(
		PluginServices.getText(this,"insert_first_point"));
		setDescription(new String[]{"cancel"});
		}
	Exit{
		}

	{
		addPoint( pointX:double,pointY:double,event:InputEvent)
			NextPoint{
				setQuestion(PluginServices.getText(this,"insert_next_point_arc_or_close"));
				setDescription(new String[]{"inter_arc","close_polyline","terminate","cancel"});
				addPoint( pointX,pointY,event);
				}
	}
NextPoint{

	addOption(s:String)
		[s.equals("C") ||  s.equals("c") || s.equals(PluginServices.getText(this,"close_polyline"))]
		FirstPoint {
			addOption(s);
			closeGeometry();
			endGeometry();
			end();
			}
	addOption(s:String)
		[s.equals("T") ||  s.equals("t") || s.equals(PluginServices.getText(this,"terminate"))]
		FirstPoint {
			addOption(s);
			endGeometry();
			end();
			}
	addPoint( pointX:double,pointY:double,event:InputEvent)
		NextPoint {
			setQuestion(PluginServices.getText(this,"insert_next_point_arc_or_close"));
			setDescription(new String[]{"close_polyline","terminate","cancel"});
			addPoint( pointX,pointY,event);
			}
}

Default
{
	addOption(s:String)
		[s.equals(PluginServices.getText(this,"cancel"))]
		FirstPoint{
			cancel();
			}
	addOption(s:String)
		[s.equals("")]
		FirstPoint{
			endGeometry();
			}
	addOption(s:String)
		FirstPoint{
			throwOptionException(PluginServices.getText(this,"incorrect_option"),s);
			}
	addValue(d:double)
		FirstPoint{
			throwValueException(PluginServices.getText(this,"incorrect_value"),d);
			}
	addPoint(pointX:double,pointY:double,event:InputEvent)
		FirstPoint{
			throwPointException(PluginServices.getText(this,"incorrect_point"),pointX,pointY);
			}

}
%%
