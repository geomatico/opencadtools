// -*- tab-width: 4; -*-

%{
//
// Vicente Caballero Navarro
%}

%start Selection::FirstPoint
%class SelectionCADTool
%package com.iver.cit.gvsig.gui.cad.tools.smc
%import com.iver.cit.gvsig.gui.cad.tools.SelectionCADTool
%import com.iver.cit.gvsig.fmap.layers.FBitSet
%import java.awt.event.InputEvent


%map Selection
%%
// A task begins life in suspended animation.

FirstPoint

	Entry {
		setQuestion("SELECCION" + "\n" +
		"Precise punto del rectángulo de selección");
		setDescription(new String[]{"Cancelar"});
		}
	Exit{
		}

	{
		addPoint( pointX:double,pointY:double,event:InputEvent)
			[ctxt.selectFeatures(pointX,pointY) && ctxt.getNextState().equals("Selection.SecondPoint")]
			SecondPoint{
				setQuestion("Precise segundo punto del rectángulo de seleccion");
				setDescription(new String[]{"Cancelar"});
				addPoint( pointX,pointY,event);
				}
		addPoint( pointX:double,pointY:double,event:InputEvent)
			[ctxt.getNextState().equals("Selection.WithSelectedFeatures")]
			WithSelectedFeatures {
				setQuestion("select_handlers");
				setDescription(new String[]{"Cancelar"});
				addPoint( pointX,pointY,event);
				end();
				}
}

SecondPoint{
	// Si hemos conseguido seleccionar algo
	addPoint( pointX:double,pointY:double,event:InputEvent)
			[ctxt.selectWithSecondPoint(pointX,pointY) > 0]
			WithSelectedFeatures {
				setQuestion("select_handlers");
				setDescription(new String[]{"Cancelar"});
				addPoint( pointX,pointY,event);
				end();
				}
	// Si no lo hemos conseguido
	addPoint( pointX:double,pointY:double,event:InputEvent)
			FirstPoint {
				setQuestion("Precise punto del rectángulo de selección");
				setDescription(new String[]{"Cancelar"});
				}


}
WithSelectedFeatures{

	// Si hemos pinchado encima de un handler
	addPoint( pointX:double,pointY:double,event:InputEvent)
	[ctxt.selectHandlers(pointX, pointY)>0]
			WithHandlers {
				setQuestion("Precise punto destino");
				setDescription(new String[]{"Cancelar"});
				addPoint( pointX,pointY,event);
				// refresh();
				}

	// Si no hemos cogido un handler, probamos a seleccionar
	// otra entidad
	addPoint( pointX:double,pointY:double,event:InputEvent)
		[ctxt.selectFeatures(pointX,pointY) && ctxt.getNextState().equals("Selection.WithSelectedFeatures")]
			WithSelectedFeatures {
				setQuestion("select_handlers");
				setDescription(new String[]{"Cancelar"});
				addPoint( pointX,pointY,event);
				}


	// Cualquier otro caso (hemos pinchado en algo vacio), volvemos al estado inicial
	addPoint( pointX:double,pointY:double,event:InputEvent)
			FirstPoint {
				setQuestion("Precise punto del rectángulo de selección");
				setDescription(new String[]{"Cancelar"});
				addPoint( pointX,pointY,event);
				// refresh();
				}

}

WithHandlers{
	addPoint( pointX:double,pointY:double,event:InputEvent)
			WithSelectedFeatures {
				setQuestion("select_handlers");
				setDescription(new String[]{"Cancelar"});
				addPoint( pointX,pointY,event);
				refresh();
				}
}

Default
{
	addOption(s:String)
		[s.equals("Cancelar")]
		FirstPoint{
			end();
			}
}
%%
