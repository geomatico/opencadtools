// -*- tab-width: 4; -*-

%{
//
// Vicente Caballero Navarro
%}

%start Scale::PointMain
%class ScaleCADTool
%package com.iver.cit.gvsig.gui.cad.tools.smc
%import com.iver.cit.gvsig.gui.cad.tools.ScaleCADTool

%map Scale
%%
// A task begins life in suspended animation.

PointMain
	Entry {
		selection();
		setQuestion("ESCALAR" + "\n" +
		"Precise punto base");
		setDescription(new String[]{"Cancelar"});

		}
	Exit{
		}

	{
		addPoint(pointX:double,pointY:double)
			ScaleFactorOrReference {
				setQuestion("Precise factor de escala<2> o Referencia[R]");
				setDescription(new String[]{"Referencia","Cancelar"});
				addPoint(pointX,pointY);
				}
	}
ScaleFactorOrReference{
	addValue(d:double)
		PointMain{
			addValue(d);
			end();
			refresh();
			}
	addOption(s:String)
		[s.equals(null) || s.equals("")]
		PointMain{
			addOption(s);
			end();
			refresh();
			}
	addOption(s:String)
		[s.equals("R") || s.equals("r") || s.equals("Referencia")]
		PointOriginOrScaleFactor{
			setQuestion("Precise punto origen recta referencia o Factor de escala[F]");
			setDescription(new String[]{"Factor escala","Cancelar"});
			addOption(s);
			}
	addPoint(pointX:double,pointY:double)
		PointMain {
			addPoint(pointX,pointY);
			end();
			refresh();
			}
}
PointOriginOrScaleFactor{
	addOption(s:String)
		[s.equals("F") || s.equals("f") || s.equals("Factor escala")]
		PointMain{
			setQuestion("Precise factor de escala<2> o Referencia[R]");
			setDescription(new String[]{"Referencia","Cancelar"});
			addOption(s);
			}
	addPoint(pointX:double,pointY:double)
		EndPointReference {
			setQuestion("Precise punto final recta referencia");
			setDescription(new String[]{"Cancelar"});
			addPoint(pointX,pointY);
			}
}
EndPointReference{
	addPoint(pointX:double,pointY:double)
		OriginPointScale {
			setQuestion("Precise punto origen recta escala");
			setDescription(new String[]{"Cancelar"});
			addPoint(pointX,pointY);
			}
}
OriginPointScale{
	addPoint(pointX:double,pointY:double)
		EndPointScale {
			setQuestion("Precise punto final recta escala");
			setDescription(new String[]{"Cancelar"});
			addPoint(pointX,pointY);
			}
}
EndPointScale{
	addPoint(pointX:double,pointY:double)
		PointMain {
			addPoint(pointX,pointY);
			end();
			refresh();
			}
}

Default
{
	addOption(s:String)
		[s.equals("Cancelar")]
		PointMain{
			end();
			}
}
%%
