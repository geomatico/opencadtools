// -*- tab-width: 4; -*-

%{
//
// Vicente Caballero Navarro
%}

%start Polygon::NumberOrCenterPoint
%class PolygonCADTool
%package com.iver.cit.gvsig.gui.cad.tools.smc
%import com.iver.cit.gvsig.gui.cad.tools.PolygonCADTool
%import java.awt.event.InputEvent
%import com.iver.andami.PluginServices

%map Polygon
%%
// A task begins life in suspended animation.

NumberOrCenterPoint

	Entry {
		setQuestion(ctxt.getName() + "\n" +
		PluginServices.getText(this,"insert_number_sides"));
		setDescription(new String[]{"cancel"});
		}
	Exit{
		}

	{
		addValue(d:double)
			CenterPoint {
				setQuestion(PluginServices.getText(this,"insert_central_point_polygon"));
				setDescription(new String[]{"cancel"});
				addValue(d);
				}
		addPoint( pointX:double,pointY:double,event:InputEvent)
			OptionOrRadiusOrPoint {
				setQuestion(PluginServices.getText(this,"into_circle_or_circumscribed"));
				setDescription(new String[]{"into_circle","circumscribed","cancel"});
				addPoint( pointX,pointY,event);
				}

	}
CenterPoint{
	addPoint( pointX:double,pointY:double,event:InputEvent)
		OptionOrRadiusOrPoint {
			setQuestion(PluginServices.getText(this,"into_circle_or_circumscribed"));
			setDescription(new String[]{"into_circle","circumscribed","cancel"});
			addPoint( pointX,pointY,event);
			}
}
OptionOrRadiusOrPoint{
	addOption(s:String)
		RadiusOrPoint {
			setQuestion(PluginServices.getText(this,"insert_radius"));
			setDescription(new String[]{"cancel"});
			addOption(s);
			}
	addValue(d:double)
		NumberOrCenterPoint{
			addValue(d);
			end();
			}
	addPoint( pointX:double,pointY:double,event:InputEvent)
		NumberOrCenterPoint {
			addPoint( pointX,pointY,event);
			end();
			}
}
RadiusOrPoint{
	addPoint( pointX:double,pointY:double,event:InputEvent)
		NumberOrCenterPoint {
			addPoint( pointX,pointY,event);
			end();
			}
	addValue(d:double)
		NumberOrCenterPoint{
			addValue(d);
			end();
			}
}

Default
{
	addOption(s:String)
		[s.equals(PluginServices.getText(this,"cancel"))]
		NumberOrCenterPoint{
			end();
			}
}
%%
