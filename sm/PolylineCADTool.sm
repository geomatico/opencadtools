// -*- tab-width: 4; -*-

%{
//
// Vicente Caballero Navarro
%}

%start Polyline::FirstPoint
%class PolylineCADTool
%package com.iver.cit.gvsig.gui.cad.tools.smc
%import com.iver.cit.gvsig.gui.cad.tools.PolylineCADTool
%import java.awt.event.InputEvent
%import com.iver.andami.PluginServices

%map Polyline
%%
// A task begins life in suspended animation.

FirstPoint

	Entry {
		setQuestion(ctxt.getName() + "\n" +
		PluginServices.getText(this,"insert_first_point"));
		setDescription(new String[]{"cancel"});
		}
	Exit{
		}

	{
		addPoint( pointX:double,pointY:double,event:InputEvent)
			NextPointOrArcOrClose{
				setQuestion(PluginServices.getText(this,"insert_next_point_arc_or_close"));
				setDescription(new String[]{"arc","close","cancel"});
				addPoint( pointX,pointY,event);
				}
	}
NextPointOrArcOrClose{

	addOption(s:String)
		[s.equals("A") ||  s.equals("a") || s.equals(PluginServices.getText(this,"arc"))]
		NextPointOrLineOrClose {
			setQuestion(PluginServices.getText(this,"insert_next_point_line_or_close"));
			setDescription(new String[]{"line","close","cancel"});
			addOption(s);
			}
	addOption(s:String)
		[s.equals("C") ||  s.equals("c") || s.equals(PluginServices.getText(this,"close"))]
		FirstPoint {
			addOption(s);
			closeGeometry();
			endGeometry();
			end();
			}
	addPoint( pointX:double,pointY:double,event:InputEvent)
		NextPointOrArcOrClose {
			setQuestion(PluginServices.getText(this,"insert_next_point_arc_or_close"));
			setDescription(new String[]{"arc","close","cancel"});
			addPoint( pointX,pointY,event);
			}
}
NextPointOrLineOrClose{
	addPoint( pointX:double,pointY:double,event:InputEvent)
		NextPointOrLineOrClose {
			setQuestion(PluginServices.getText(this,"insert_next_point_line_or_close"));
			setDescription(new String[]{"line","close","cancel"});
			addPoint( pointX,pointY,event);
			}
	addOption(s:String)
		[s.equals("N") ||  s.equals("n") || s.equals(PluginServices.getText(this,"line"))]
		NextPointOrArcOrClose {
			setQuestion(PluginServices.getText(this,"insert_next_point_arc_or_close"));
			setDescription(new String[]{"arc","close","cancel"});
			addOption(s);
			}
	addOption(s:String)
		[s.equals("C") ||  s.equals("c") || s.equals(PluginServices.getText(this,"close"))]
		FirstPoint {
			addOption(s);
			closeGeometry();
			endGeometry();
			end();
			}
}

Default
{
	addOption(s:String)
		[s.equals(PluginServices.getText(this,"cancel"))]
		FirstPoint{
			cancel();
			}
	addOption(s:String)
		[s.equals("")]
		FirstPoint{
			endGeometry();
			}

}
%%
