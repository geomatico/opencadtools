// -*- tab-width: 4; -*-

%{
//
// Vicente Caballero Navarro
%}

%start Polyline::FirstPoint
%class PolylineCADTool
%package com.iver.cit.gvsig.gui.cad.tools.smc
%import com.iver.cit.gvsig.gui.cad.tools.PolylineCADTool
%import java.awt.event.InputEvent

%map Polyline
%%
// A task begins life in suspended animation.

FirstPoint

	Entry {
		setQuestion("POLILINEA" + "\n" +
		"Insertar primer punto");
		setDescription(new String[]{"Cancelar"});
		}
	Exit{
		}

	{
		addPoint( pointX:double,pointY:double,event:InputEvent)
			NextPointOrArcOrClose{
				setQuestion("Insertar siguiente punto, Arco[A] o Cerrar[C]");
				setDescription(new String[]{"Arco","Cerrar","Cancelar"});
				addPoint( pointX,pointY,event);
				}
	}
NextPointOrArcOrClose{

	addOption(s:String)
		[s.equals("A") ||  s.equals("a") || s.equals("Arco")]
		NextPointOrLineOrClose {
			setQuestion("Insertar punto siguiente, Linea[N] o Cerrar[C]");
			setDescription(new String[]{"Linea","Cerrar","Cancelar"});
			addOption(s);
			}
	addOption(s:String)
		[s.equals("C") ||  s.equals("c") || s.equals("Cerrar")]
		FirstPoint {
			addOption(s);
			closeGeometry();
			endGeometry();
			end();
			}
	addPoint( pointX:double,pointY:double,event:InputEvent)
		NextPointOrArcOrClose {
			setQuestion("Insertar siguiente punto, Arco[A] o Cerrar[C]");
			setDescription(new String[]{"Arco","Cerrar","Cancelar"});
			addPoint( pointX,pointY,event);
			}
}
NextPointOrLineOrClose{
	addPoint( pointX:double,pointY:double,event:InputEvent)
		NextPointOrLineOrClose {
			setQuestion("Insertar punto siguiente, Linea[N] o Cerrar[C]");
			setDescription(new String[]{"Linea","Cerrar","Cancelar"});
			addPoint( pointX,pointY,event);
			}
	addOption(s:String)
		[s.equals("N") ||  s.equals("n") || s.equals("Linea")]
		NextPointOrArcOrClose {
			setQuestion("Insertar siguiente punto, Arco[A] o Cerrar[C]");
			setDescription(new String[]{"Arco","Cerrar","Cancelar"});
			addOption(s);
			}
	addOption(s:String)
		[s.equals("C") ||  s.equals("c") || s.equals("Cerrar")]
		FirstPoint {
			addOption(s);
			closeGeometry();
			endGeometry();
			end();
			}
}

Default
{
	addOption(s:String)
		[s.equals("Cancelar")]
		FirstPoint{
			cancel();
			}
	addOption(s:String)
		[s.equals("")]
		FirstPoint{
			endGeometry();
			}

}
%%
